-- Example of using algebraic effects and handlers in Granule to capture
-- state effects

-- Uses the builtin `call` and `handle` operations.
-- See https://granule-project.github.io/docs/modules/Primitives.html#algebraiceffectsandhandlers

import State

-- # State operations

data Labels = Get | Put

-- Operations
data StateOps (s : Type) (r : Type) : Set Labels -> Type where
 GetOp : ()         -> (s  -> r) [1] -> StateOps s r {Get};
 PutOp : s [0..Inf] -> (() -> r) [1] -> StateOps s r {Put}

 -- Functoriality of operations
fmap_stateops : forall {s a b : Type, l : Set Labels}
              . (a -> b) [0..Inf] -> StateOps s a l -> StateOps s b l
fmap_stateops [f] (GetOp () [k]) = GetOp () [f . k];
fmap_stateops [f] (PutOp [s] [k]) = PutOp [s] [f . k]

-- Handler to state monad
stateHandler : forall {s r : Type, l : Set Labels}
             . StateOps s (State s r) l -> State s r
stateHandler (GetOp () [k]) = join_state (State (\([s] : s [0..Inf]) -> (k s, [s])));
stateHandler (PutOp s [k])  = join_state (State (\([_] : s [0..Inf]) -> (k (), s)))

-- # Examples

incr : Int <Eff Labels (StateOps Int) {Get, Put}>
incr = let
  y   <- call GetOp ();
  [z] <- pure (moveInt y);
  ()  <- call PutOp [z + 1] in
  pure z

-- Handle state wrapped
handleState : forall {a b : Type, e : Set Labels, s : Type}
            . a <Eff Labels (StateOps s) e>
           -> State s a
handleState = handle (/\{a,b,l}.fmap_stateops) [/\{l}.stateHandler] (return_state)

main : (Int, Int [])
main = runState (handleState incr) [42]